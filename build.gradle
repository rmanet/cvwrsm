// This is the root build.gradle, which defines a few project-wide properties.
// Notably, this project sets the version of all projects, as well as the group name.

plugins {
    id "com.palantir.git-version" version "3.0.0"
    id 'base'
}

// This utilizes Git Tag based versioning to set the version of the project based on the current Git Branch or Tag.
// If a tag is checked out, the version will be the name of the most recent tag on this commit.
// Otherwise, the version will be branchname-SNAPSHOT.
def versionLabel(gitInfo) {
    def branch = gitInfo.branchName // all branches are snapshots, only tags get released
    def tag = gitInfo.lastTag
    // tag is returned as is. Branch may need cleanup
    return branch == null ? tag : branch.replace("/","-") + "-SNAPSHOT"
}

allprojects {
    group = 'gov.ca.dwr'
    version = versionLabel(versionDetails())
}